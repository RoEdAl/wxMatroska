#
# Installer.cmake
#

# installer code - create pkg-desc.json

STRING(CONFIGURE [=[
SET(_PKG_DESC "{}")
STRING(JSON _PKG_DESC SET "${_PKG_DESC}" buildConfig "\"$<CONFIG>\"")
STRING(JSON _PKG_DESC SET "${_PKG_DESC}" packageName "\"cue2mkc\"")
CMAKE_PATH(APPEND CMAKE_INSTALL_PREFIX pkg-desc.json OUTPUT_VARIABLE _PKG_DESC_PATH)
FILE(WRITE "${_PKG_DESC_PATH}" "${_PKG_DESC}")
]=] INSTALL_SCRIPT @ONLY)
INSTALL(CODE "${INSTALL_SCRIPT}" COMPONENT cue2mkc EXCLUDE_FROM_ALL)

# install & strip DLLs
IF(MINGW)
	FIND_PROGRAM(STRIP_EXE "strip")
	IF(NOT STRIP_EXE)
		MESSAGE(FATAL_ERROR "Strip utility not found")
	ENDIF()
	STRING(CONFIGURE [=[
FOREACH(LIBPATH $<TARGET_RUNTIME_DLLS:wxCueChapters>)
	CMAKE_PATH(GET LIBPATH FILENAME LIBNAME)
	EXECUTE_PROCESS(COMMAND "@STRIP_EXE@" --preserve-dates $<IF:$<CONFIG:Debug,RelWithDebInfo>,--only-keep-debug,--strip-all> "${LIBPATH}" -o ${LIBNAME} ENCODING UTF-8 WORKING_DIRECTORY ${CMAKE_INSTALL_PREFIX}/cue2mkc COMMAND_ERROR_IS_FATAL ANY)
ENDFOREACH()
]=] INSTALL_SCRIPT @ONLY)
	INSTALL(CODE "${INSTALL_SCRIPT}" COMPONENT cue2mkc EXCLUDE_FROM_ALL)
ENDIF()

# CPack

SET(CPACK_PACKAGE_NAME cue2mkc)
SET(CPACK_PACKAGE_VENDOR "Edmunt Pienkowsky")
SET(CPACK_PACKAGE_ICON ../gui/icons/cd_mka.ico)
LIST(APPEND CPACK_GENERATOR External)
LIST(APPEND CPACK_SOURCE_GENERATOR ZIP)
LIST(APPEND CPACK_SOURCE_IGNORE_FILES "/\.editorconfig")
LIST(APPEND CPACK_SOURCE_IGNORE_FILES "/\.gitignore")
LIST(APPEND CPACK_SOURCE_IGNORE_FILES "/\.git/")
LIST(APPEND CPACK_SOURCE_IGNORE_FILES "/doc/")
LIST(APPEND CPACK_SOURCE_IGNORE_FILES "/cue/")
LIST(APPEND CPACK_SOURCE_IGNORE_FILES "/CMakeUserPresets.*\.json")
SET(CPACK_EXTERNAL_ENABLE_STAGING ON)
SET(CPACK_PACKAGE_CHECKSUM SHA1)
SET(CPACK_COMPONENTS_ALL cue2mkc)
SET(CPACK_PROJECT_CONFIG_FILE ${CMAKE_BINARY_DIR}/cpack.cmake)
SET(CPACK_INSTALL_CMAKE_PROJECTS "${CMAKE_BINARY_DIR};${CMAKE_PROJECT_NAME};cue2mkc;/")
CMAKE_PATH(APPEND CMAKE_SOURCE_DIR cpack InnoSetup.cmake OUTPUT_VARIABLE CPACK_EXTERNAL_PACKAGE_SCRIPT)

CONFIGURE_FILE(
	${CMAKE_SOURCE_DIR}/cpack/cpack.cmake.in
	${CMAKE_BINARY_DIR}/cpack.cmake
	@ONLY
)

INCLUDE(CPack)
