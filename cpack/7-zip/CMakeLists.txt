#
# 7-zip standalone executable
# unpacker required by installer
#

SET(SEVENZIP_EXTRA "7z2201-extra")
SET(SEVENZIP_EXTRA_CHECKSUM fb776489799cd5ca0e151830cf2e6a9819c5c16c8e7571ff706aeeee07da2883)

IF(EXISTS ${CMAKE_CURRENT_BINARY_DIR}/${SEVENZIP_EXTRA}.7z)
	FILE(SHA256 ${CMAKE_CURRENT_BINARY_DIR}/${SEVENZIP_EXTRA}.7z SEVENZIP_FILE_CHECKSUM)
	IF(NOT(${SEVENZIP_EXTRA_CHECKSUM} STREQUAL ${SEVENZIP_FILE_CHECKSUM}))
		FILE(REMOVE ${CMAKE_CURRENT_BINARY_DIR}/${SEVENZIP_EXTRA}.7z)
	ENDIF()	
ENDIF()

IF(NOT EXISTS ${CMAKE_CURRENT_BINARY_DIR}/${SEVENZIP_EXTRA}.7z)
	FILE(DOWNLOAD
		"https://www.7-zip.org/a/${SEVENZIP_EXTRA}.7z"
		${CMAKE_CURRENT_BINARY_DIR}/${SEVENZIP_EXTRA}.7z
		EXPECTED_HASH SHA256=${SEVENZIP_EXTRA_CHECKSUM}
	)
ENDIF()

IF(NOT EXISTS ${CMAKE_CURRENT_BINARY_DIR}/7-zip)
	FILE(ARCHIVE_EXTRACT 
		INPUT ${CMAKE_CURRENT_BINARY_DIR}/${SEVENZIP_EXTRA}.7z
		DESTINATION ${CMAKE_CURRENT_BINARY_DIR}/7-zip
	)
ENDIF()

IF(${EX_PLATFORM} EQUAL 32)
	INSTALL(
		FILES ${CMAKE_CURRENT_BINARY_DIR}/7-zip/7za.exe
		DESTINATION cue2mkc
		COMPONENT cue2mkc
		EXCLUDE_FROM_ALL
	)
ELSE()
	INSTALL(
		FILES ${CMAKE_CURRENT_BINARY_DIR}/7-zip/x64/7za.exe
		DESTINATION cue2mkc
		COMPONENT cue2mkc
		EXCLUDE_FROM_ALL
	)
ENDIF()
