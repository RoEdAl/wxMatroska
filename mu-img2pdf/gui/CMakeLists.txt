#
# img2pdfGUI
#
FILE(GLOB IMG2PDF_GUI_HEADER_FILES "*.h")
FILE(GLOB IMG2PDF_GUI_ICON_FILES "../gui/icons/*.ico" )
GET_FILENAME_COMPONENT(IMG2PDF_GUI_DIR_NAME ${CMAKE_CURRENT_SOURCE_DIR} NAME)

SOURCE_GROUP("Resource Files" FILES Img2PdfGui.rc ${IMG2PDF_GUI_ICON_FILES})

SET(IMG2PDF_GUI_SOURCE_FILES
	wxApp.cpp
	wxMainFrame.cpp
	LogListBox.cpp
	VariantExt.cpp
)

ADD_EXECUTABLE(wxImg2PdfGui WIN32
	${IMG2PDF_GUI_SOURCE_FILES}
	${IMG2PDF_GUI_HEADER_FILES}
	Img2PdfGui.rc
	${IMG2PDF_GUI_ICON_FILES}
	${PROJECT_CONFIG_FILE}
)

ConfigureWxMatroska(wxImg2PdfGui)
ConfigureWxWidgetsUsage(wxImg2PdfGui)
ConfigureSourceEncoding(wxImg2PdfGui)
SET_PROPERTY(TARGET wxImg2PdfGui PROPERTY OUTPUT_NAME img2pdf-frontend)
TARGET_LINK_LIBRARIES(wxImg2PdfGui
	PRIVATE wxCmdTools wxEncodingDetection nlohmann_json::nlohmann_json
)

SET(MANIFEST_DEFS wxUSE_RC_MANIFEST=1 wxUSE_DPI_AWARE_MANIFEST=2)
SET_SOURCE_FILES_PROPERTIES(Img2PdfGui.rc PROPERTIES COMPILE_DEFINITIONS "${MANIFEST_DEFS}")
IF(DEFINED WXWIDGETS_CFG_FILE)
	ADD_CUSTOM_COMMAND(TARGET wxImg2PdfGui POST_BUILD
		COMMAND ${CMAKE_COMMAND} -E copy_if_different $<TARGET_RUNTIME_DLLS:wxImg2PdfGui> $<TARGET_FILE_DIR:wxImg2PdfGui>
		COMMAND_EXPAND_LISTS
	)
ELSE()
	ADD_CUSTOM_COMMAND(TARGET wxImg2PdfGui POST_BUILD
		COMMAND ${CMAKE_COMMAND} -E copy_if_different 
			${wxWidgets_LIB_DIR}/${CMAKE_SHARED_LIBRARY_PREFIX}wxbase${WXWIDGETS_VER_COMPACT}u$<$<CONFIG:Debug>:d>_${WXWIDGETS_DLL_SUFFIX}${CMAKE_SHARED_LIBRARY_SUFFIX}
			${wxWidgets_LIB_DIR}/${CMAKE_SHARED_LIBRARY_PREFIX}wxbase${WXWIDGETS_VER_COMPACT}u$<$<CONFIG:Debug>:d>_xml_${WXWIDGETS_DLL_SUFFIX}${CMAKE_SHARED_LIBRARY_SUFFIX}
			${wxWidgets_LIB_DIR}/${CMAKE_SHARED_LIBRARY_PREFIX}wxmsw${WXWIDGETS_VER_COMPACT}u$<$<CONFIG:Debug>:d>_core_${WXWIDGETS_DLL_SUFFIX}${CMAKE_SHARED_LIBRARY_SUFFIX}
			$<TARGET_FILE_DIR:wxImg2PdfGui>
	)
ENDIF()

IF(DEFINED MINGW_CFG_FILE)
	ADD_CUSTOM_COMMAND(TARGET wxImg2PdfGui POST_BUILD
		COMMAND ${CMAKE_COMMAND} -E copy_if_different $<TARGET_PROPERTY:GNU_gcc,IMPORTED_LOCATION> $<TARGET_PROPERTY:GNU_stdcxx,IMPORTED_LOCATION> $<TARGET_FILE_DIR:wxImg2PdfGui>
	)
ENDIF()

SET_PROPERTY(TARGET wxImg2PdfGui PROPERTY FOLDER img2pdf)

INSTALL(
	TARGETS wxImg2PdfGui
	DESTINATION img2pdf
	COMPONENT img2pdf
)
